## Productions for all agents, regarding common survival
## and interaction with the environment

## In summary, describes only one action: eating.
## (in environment, agents that reach food-satiety 0 are killed)

# INIT
sp {AoE*general_agent*init
   (state <s> ^superstate nil)
-->
   (<s> ^inventory <inv1>)
   (<inv1> ^name inventory)
   (<s> ^hunger <hun1>)
   (<hun1> ^name hunger)
}

## ELABORATION #########################


# Elaborate whether we have food amount
sp {AoE*general_agent*elaborate*has-food
   (state <s> ^io <io>
              ^inventory <I>
   )
   (<io> ^input-link.food > 0)
-->
   (<I> ^has-food true)
   (write |Food detected in inventory | <I> (crlf))
}

# Elaborate how hungry we are by peeking at input
sp {AoE*general_agent*elaborate*moderate-hunger
   (state <s> ^io.input-link.food-satiety < 20
              ^hunger <h>
   )
-->
   (<h> ^hungry true)
}

sp {AoE*general_agent*elaborate*starving
   (state <s> ^io.input-link.food-satiety < 5
              ^hunger <h>
   )
-->
   (<h> ^starving true)
}






## PROPOSAL #########################


# If we have food, we can eat it
sp {AoE*general_agent*propose*eat-food
   (state <s> ^inventory <I>)
   (<I> ^has-food true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name eat-food)
}


## SELECTION #########################

# If we are moderately hungry, we prefer (10) to eat
sp {AoE*general_agent*select*operator*prefer*eat-food-hungry
   (state <s> ^hunger <h>
              ^operator <o> +) # acceptable operator
   (<o> ^name eat-food)
   (<h> ^hungry true)
-->
   (<s> ^operator <o> = 10)
}

# If we are extremely hungry, we must eat (not using required because of debugging circumstances)
sp {AoE*general_agent*select*operator*prefer*eat-food-starving
   (state <s> ^hunger <h>
              ^operator <o> +) # acceptable operator
   (<o> ^name eat-food)
   (<h> ^starving true)
-->
   (<s> ^operator <o> >)
}


## APPLICATION #########################


# If we pick to eat, we request the environment to do so
# (environment should remove our request from pipeline)
sp {AoE*general_agent*apply*operator*eat-food
   (state <s> ^io.output-link <out>
              ^operator.name eat-food)
  -(<out> ^eat-food)
-->
   (<out> ^eat-food true)
}

sp {AoE*general_agent*monitor*operator*eat-food
   (state <s> ^operator.name eat-food)
-->
   (write |Requested environment to eat food| (crlf))
}