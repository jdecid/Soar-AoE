## Productions for collector agents

## Collector agents can get food/wood
## Collector agents should give up their resources if the lord asks
##   (but will prioritise not dying of starvation)
## Collector agents can get wood by just acting on the environment
## Collector agents can get food by sowing-waiting-harvesting
## Collector agents like to stack up on resources

## INIT #########################

sp {AoE*collector_agent*init
   (state <s> ^superstate nil)
-->
   (<s> ^fields <f>)
   (<f> ^name fields)
}

## ELABORATION #########################
sp {AoE*general_agent*elaborate*has-wood
   (state <s> ^io <io>
              ^inventory <I>
   )
   (<io> ^input-link.wood > 0)
-->
   (<I> ^has-wood true)
   (write |Wood detected in inventory | <I> (crlf))
}

# Elaborate whether the wood inventory is full
sp {AoE*collector*elaborate*wood-inventory-full
   (state <s> ^io <io>
              ^inventory <I>
   )
   (<io> ^input-link.wood = 5)
-->
   (<I> ^wood-full true)
}

# Elaborate whether the food inventory is full
sp {AoE*collector*elaborate*food-inventory-full
   (state <s> ^io <io>
              ^inventory <I>
   )
   (<io> ^input-link.food = 5)
-->
   (<I> ^food-full true)
}

# Elaborate whether I have fields, and their state (copy from input)
sp {AoE*collector*elaborate*has-fields
   (state <s> ^io <io>
              ^fields <f>
   )
   (<io> ^input-link.fields <field>)
-->
   (<f> ^field <field>)
}




## PROPOSAL #########################

# If our wood inventory is not full, we can get wood
sp {AoE*collector*propose*collect-wood
   (state <s> ^inventory <I>)
   -(<I> ^wood-full true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name cut-wood)
}

# If one of our fields is dry (not sown nor harvestable), we can sow it.
sp {AoE*collector*propose*sow-field
   (state <s> ^fields.field <f>)
   (<f> ^state dry)
-->
   (<s> ^operator <o> + =)
   (<o> ^name sow-field 
        ^field <f>)
}

# If one of our fields is harvestable, we can harvest it if we're not food-full
#   but we can harvest it even if we can't get all of the yield.
sp {AoE*collector*propose*harvest-field
   (state <s> ^fields.field <f>
   			  ^inventory <I>
   )
   (<f> ^state harvestable)
   -(<I> ^food-full true)
-->
   (<s> ^operator <o> + =)
   (<o> ^name harvest-field 
        ^field <f>)
}


# TODO: PROPOSE FULFILL DEMAND from lord




## SELECTION #########################

# General guidelines:

# if no food, best thing to do is harvest some
sp {AoE*collector*prefer*harvest-food-if-none
   (state <s> ^inventory <I>
   			  ^operator <o> +)
   (<o> ^name harvest-field)
   -(<I> ^has-food true)
-->
   (<s> ^operator <o> >) # best operator (specific better-worse come before)
}

# if no field sown, best thing to do is sow one
sp {AoE*collector*prefer*sow-food-if-none-growing-or-harvestable
   (state <s> ^fields <f>
   			  ^operator <o> +)
   (<o> ^name sow-field)
   -{ # not exists a field that is in f and is harvestable or sown
	   (<f> ^field <f1>)
	   (<f1> ^state << harvestable sown >>)
   }
-->
   (<s> ^operator <o> >) # best operator (specific better-worse come before)
}


# if no wood, good thing to do is collect some
sp {AoE*collector*prefer*collect-wood-if-none
   (state <s> ^inventory <I>
   			  ^operator <o> +)
   (<o> ^name cut-wood)
   -(<I> ^has-wood true)
-->
   (<s> ^operator <o> = 10) # pretty good pick
}


# Comparative guidelines:

# We prefer to sow high-yield fields over low-yield
sp {AoE*collector*prefer*high-yield-sow-over-low
   (state <s> ^fields.field <f1> <f2>
   			  ^operator <o1> +
   			  ^operator <o2> +)
   (<o1> ^name sow-field
         ^field <f1>
   )
   (<o2> ^name sow-field
         ^field <f2>
   )
   (<f1> ^yield <y1>)
   (<f2> ^yield <y2> < <y1>)
-->
   (<s> ^operator <o1> > <o2>) # pick sowing the good field
}

# TODO: lower acceptability of harvesting when would overflow food